buildscript {
    repositories {
      maven {
        url "https://plugins.gradle.org/m2/"
      }
      mavenCentral()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:2.1.4.RELEASE')
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.21.0')
        classpath('com.avast.gradle:gradle-docker-compose-plugin:0.9.1')
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker-compose'
apply plugin: 'com.palantir.docker'

group = 'devgoon'

bootJar {
    baseName = 'spring-boot-docker'
    version =  '0.1.0'
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}
docker {
    name "${project.group}/${bootJar.baseName}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}
repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile('io.sixhours:memcached-spring-boot-starter:1.3.1')
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

dockerCompose {
    useComposeFiles = ['docker-compose.yaml']
    waitForTcpPorts = true
}
dockerCompose.isRequiredBy(test)

